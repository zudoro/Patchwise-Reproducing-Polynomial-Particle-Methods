MODULE EXACTSOL

	USE NEWTYPE
	USE GLBVAR
	
	IMPLICIT NONE
	
CONTAINS

! NONDIMENSIONAL VARIABLES ARE USED TO PRESENT THE NUMERICAL RESULTS
SUBROUTINE EXNONDIMRESULT(MAXWBAR, W_HAT, STRESS_BAR, W0, EXSTRESS, NUMGRID)
	REAL*8, INTENT(IN) :: W0(:)
	TYPE(STRESS), INTENT(IN) :: EXSTRESS(:)
	INTEGER, INTENT(IN) :: NUMGRID(2)
	REAL*8, INTENT(OUT) :: MAXWBAR, W_HAT
	TYPE(STRESS), INTENT(OUT) :: STRESS_BAR
	REAL*8 :: LENGTH_B
	INTEGER :: INDICE(6)

	LENGTH_B = DABS(OMEGA%PT4%Y - OMEGA%PT1%Y)

!	W_BAR_CENTER = (E(2)*h**3 / (LENGTH_B**4*LOAD_Q))*W0(NUMGRID(2)*(INT(0.5*NUMGRID(1)-1))+INT(0.5*NUMGRID(2)))

	INDICE = (/ MAXLOC(W0(:)), MAXLOC(EXSTRESS(:)%SXX), MAXLOC(EXSTRESS(:)%SYY), MAXLOC(EXSTRESS(:)%SXY), &
			 	MAXLOC(EXSTRESS(:)%SXZ), MAXLOC(EXSTRESS(:)%SYZ) /)

	MAXWBAR = W0(INDICE(1))*(STRESS_D(1,1)/(LOAD_Q*L**4))
	
	W_HAT = W0(INDICE(1))*100.0D0*(STRESS_D(2,2) / (LENGTH_B**4*LOAD_Q))
	STRESS_BAR%SXX = EXSTRESS(INDICE(2))%SXX*(h**2 / (LENGTH_B**2*LOAD_Q))
	STRESS_BAR%SYY = EXSTRESS(INDICE(3))%SYY*(h**2 / (LENGTH_B**2*LOAD_Q))
	STRESS_BAR%SXY = EXSTRESS(INDICE(4))%SXY*(h**2 / (LENGTH_B**2*LOAD_Q))
	STRESS_BAR%SXZ = EXSTRESS(INDICE(5))%SXZ*(h / (LENGTH_B*LOAD_Q))
	STRESS_BAR%SYZ = EXSTRESS(INDICE(6))%SYZ*(h / (LENGTH_B*LOAD_Q))

END SUBROUTINE EXNONDIMRESULT

! SIMPLY SUPPORTED RECTANGULAR PLATE WITH CONSTANT TRANSVERSE LOAD, BENDING PROBLEM
SUBROUTINE NAVIERSOL(EXDISP, EXMOMENT, EXSTRESS, XCORDS, YCORDS, M, N)

	REAL*8, INTENT(IN) :: XCORDS(:), YCORDS(:)
	INTEGER, INTENT(IN) :: M, N
	TYPE(DISPLACEMENT), INTENT(OUT) :: EXDISP(:)
	TYPE(MOMENT), INTENT(OUT) :: EXMOMENT(:)
	TYPE(STRESS), INTENT(OUT) :: EXSTRESS(:)
	INTEGER :: NUM_XCORDS, NUM_YCORDS, VEC_N
	REAL*8 :: WMN, XMN, YMN
	REAL*8 :: QMN, TMN
	REAL*8 :: ALPHA, BETA, S(3,3), B(0:2), BMN
	REAL*8 :: LENGTH_A, LENGTH_B
	REAL*8 :: TRIGOMETRIC(4)
	INTEGER :: I, J, II, JJ, KK, III
	
	NUM_XCORDS = UBOUND(XCORDS,1)
	NUM_YCORDS = UBOUND(YCORDS,1)
	VEC_N = NUM_XCORDS*NUM_YCORDS
	
	DO I=1, VEC_N
		EXDISP(I) = DISPLACEMENT(0.0D0, 0.0D0, 0.0D0)
		EXMOMENT(I) = MOMENT(0.0D0, 0.0D0, 0.0D0)
		EXSTRESS(I) = STRESS(0.0D0, 0.0D0, 0.0D0, 0.0D0, 0.0D0)
	ENDDO

	LENGTH_A=DABS(OMEGA%PT2%X - OMEGA%PT1%X)
	LENGTH_B=DABS(OMEGA%PT4%Y - OMEGA%PT1%Y)

	! STATIC, NO THERMAL BENDING ANALYSIS
	DO I=1, M
		ALPHA=I*PI/LENGTH_A
		DO J=1, N
			BETA=J*PI/LENGTH_B
		
			! STATIC SOLUTION
			S(1,1)=KS*(SHEAR_A(2,2)*ALPHA**2 + SHEAR_A(1,1)*BETA**2)+HOOKS_K
		
			S(1,2)=KS*SHEAR_A(2,2)*ALPHA
			S(1,3)=KS*SHEAR_A(1,1)*BETA
			S(2,2)=STRESS_D(1,1)*ALPHA**2 + STRESS_D(3,3)*BETA**2 + KS*SHEAR_A(2,2)
			S(2,3)=(STRESS_D(1,2) + STRESS_D(3,3))*ALPHA*BETA
			S(3,3)=STRESS_D(3,3)*ALPHA**2 + STRESS_D(2,2)*BETA**2 + KS*SHEAR_A(1,1)
			S(2,1)=S(1,2)
			S(3,1)=S(1,3)
			S(3,2)=S(2,3)
		
			B(0)=S(2,2)*S(3,3) - S(2,3)**2
			B(1)=S(2,3)*S(1,3) - S(1,2)*S(3,3)
			B(2)=S(1,2)*S(2,3) - S(2,2)*S(1,3)
			BMN=S(1,1)*B(0) + S(1,2)*B(1) + S(1,3)*B(2)

			QMN=(4.0D0*LOAD_Q)/(I*J*PI**2)*((-1)**J-1.0D0)*((-1)**I-1.0D0)
			TMN=0.0D0

			WMN=(B(0)/BMN)*QMN
			XMN=(B(1)/BMN)*QMN
			YMN=(B(2)/BMN)*QMN
			
			KK=1
			DO II=1, NUM_XCORDS
				DO JJ=1, NUM_YCORDS
					TRIGOMETRIC(1) = DSIN(ALPHA*XCORDS(II))*DSIN(BETA*YCORDS(JJ))
					TRIGOMETRIC(2) = DCOS(ALPHA*XCORDS(II))*DSIN(BETA*YCORDS(JJ))
					TRIGOMETRIC(3) = DSIN(ALPHA*XCORDS(II))*DCOS(BETA*YCORDS(JJ))
					TRIGOMETRIC(4) = DCOS(ALPHA*XCORDS(II))*DCOS(BETA*YCORDS(JJ))
					! NAVIER SOLUTION OF DISPLACEMENTS
					EXDISP(KK)%W0 = EXDISP(KK)%W0 + WMN*TRIGOMETRIC(1)
					EXDISP(KK)%PIX = EXDISP(KK)%PIX - XMN*TRIGOMETRIC(2)
					EXDISP(KK)%PIY = EXDISP(KK)%PIY - YMN*TRIGOMETRIC(3)
					! NAVIER SOLUTION OF BENDING MOMENTS
					EXMOMENT(KK)%MXX = EXMOMENT(KK)%MXX + (STRESS_D(1,1)*ALPHA*XMN + STRESS_D(1,2)*BETA*YMN) & 
									* TRIGOMETRIC(1)
					EXMOMENT(KK)%MYY = EXMOMENT(KK)%MYY + (STRESS_D(1,2)*ALPHA*XMN + STRESS_D(2,2)*BETA*YMN) & 
									* TRIGOMETRIC(1)
					EXMOMENT(KK)%MXY = EXMOMENT(KK)%MXY - (BETA*XMN + ALPHA*YMN) & 
									* TRIGOMETRIC(4)
					! NAVIER SOLUTION OF INPLANE AND TRANSVERSE SHEAR STRESSES USING THE CONTITUTIVE EQUATIONS
					EXSTRESS(KK)%SXX = EXSTRESS(KK)%SXX - (STRESS_Q(1,1)*ALPHA*XMN + STRESS_Q(1,2)*BETA*YMN) &
									* TRIGOMETRIC(1)
					EXSTRESS(KK)%SYY = EXSTRESS(KK)%SYY - (STRESS_Q(1,2)*ALPHA*XMN + STRESS_Q(2,2)*BETA*YMN) &
									* TRIGOMETRIC(1)
					EXSTRESS(KK)%SXY = EXSTRESS(KK)%SXY + STRESS_Q(3,3)*(BETA*XMN + ALPHA*YMN) &
									* TRIGOMETRIC(4)
					EXSTRESS(KK)%SYZ = EXSTRESS(KK)%SYZ + SHEAR_Q(1,1)*(YMN + BETA*WMN)*TRIGOMETRIC(3)
					EXSTRESS(KK)%SXZ = EXSTRESS(KK)%SXZ + SHEAR_Q(2,2)*(XMN + ALPHA*WMN)*TRIGOMETRIC(2)
					! IMPOSE BOUNDARY CONDITIONS
					IF (BCTYPE.EQ.'SSSS') THEN
						IF (XCORDS(II).EQ.OMEGA%PT1%X .OR. XCORDS(II).EQ.OMEGA%PT2%X) THEN
							EXDISP(KK)%W0 = 0.0D0
							EXDISP(KK)%PIY = 0.0D0
						ENDIF
						IF (YCORDS(JJ).EQ.OMEGA%PT1%Y .OR. YCORDS(JJ).EQ.OMEGA%PT4%Y) THEN
							EXDISP(KK)%W0 = 0.0D0
							EXDISP(KK)%PIX = 0.0D0
						ENDIF
					ELSEIF (BCTYPE.EQ.'CCCC') THEN
						IF (XCORDS(II).EQ.OMEGA%PT1%X .OR. XCORDS(II).EQ.OMEGA%PT2%X .OR. &
							YCORDS(JJ).EQ.OMEGA%PT1%Y .OR. YCORDS(JJ).EQ.OMEGA%PT4%Y) THEN
							EXDISP(KK) = DISPLACEMENT(0.0D0, 0.0D0, 0.0D0)
						ENDIF
					ENDIF
					KK=KK+1
				ENDDO
			ENDDO
		ENDDO
	ENDDO
	
	EXMOMENT(:)%MXY = STRESS_D(3,3)*EXMOMENT(:)%MXY
	
	! MAXIMUM STRESSES WERE EVALUATED AT THE LOCATIONS INDICATED AT Z = H/2 OR -H/2 FOR SXY
	EXSTRESS(:)%SXX = (0.50D0*h)*EXSTRESS(:)%SXX
	EXSTRESS(:)%SYY = (0.50D0*h)*EXSTRESS(:)%SYY
	EXSTRESS(:)%SXY = -(0.50D0*h)*EXSTRESS(:)%SXY
	
END SUBROUTINE NAVIERSOL

END MODULE EXACTSOL
