! FILE WRITTEN BY HYUNJU KIM
!
! DATE : TUE 30 2008

MODULE BOUNDARY

    USE GLBVAR
    USE NEWTYPE
	USE PACKAGE
    USE LOADFUNCTION
    
    IMPLICIT NONE

CONTAINS

SUBROUTINE INFONODE(REDUCEDOF, BD_MASK)
	INTEGER, INTENT(OUT) :: REDUCEDOF
	LOGICAL, INTENT(OUT) :: BD_MASK(3*DOF)
	
    TYPE(POINT2D) :: NODES(DOF)
    CHARACTER (LEN=2) :: EDGE(DOF)
    INTEGER :: BD_FLAG, I, J, II, JJ
    
    NODES=NODE_ALL(NUMPATCHES)

	DO I=1, DOF
		EDGE(I) = WHICH_BOUNDARY(NODES(I),OMEGA)
	ENDDO

	BD_MASK(:) = .FALSE.

    !-------------------!
    BD_FLAG = PROBLEM   !<---------- SWITCH TO CONTROL THE BOUNDARY CONDITION
    !-------------------!
    
    IF (BCTYPE.EQ.'SSSS') THEN
		!!!!!!!!!! HARD TYPE SIMPLE SUPPORT !!!!!!!!!!

		!---- W0 = 0 ON EVERY BOUNDARIES -----		
		WHERE (EDGE.NE.'NN')
			BD_MASK(1:DOF) = .TRUE.
		END WHERE
		!---- PI_X = 0 ON GAMMA 1 AND 3 -----		
		WHERE (EDGE.EQ.'FF' .OR. EDGE.EQ.'KK' .OR. & 
			   EDGE.EQ.'LF' .OR. EDGE.EQ.'RF' .OR. EDGE.EQ.'LK' .OR. EDGE.EQ.'RK')
			BD_MASK(DOF+1:2*DOF) = .TRUE.
		END WHERE
		!---- PI_Y = 0 ON GAMMA 2 AND 4 -----				
		WHERE (EDGE.EQ.'RR' .OR. EDGE.EQ.'LL' .OR. & 
			   EDGE.EQ.'LF' .OR. EDGE.EQ.'RF' .OR. EDGE.EQ.'LK' .OR. EDGE.EQ.'RK')
			BD_MASK(2*DOF+1:3*DOF) = .TRUE.
		END WHERE
	ELSEIF (BCTYPE.EQ.'CCCC') THEN
		!!!!!!!!!! HARD TYPE SIMPLE SUPPORT !!!!!!!!!!

		!---- W0 = 0, PIX = 0, PIY = 0 ON EVERY BOUNDARIES -----		
		WHERE (EDGE.NE.'NN')
			BD_MASK(1:DOF) = .TRUE.
			BD_MASK(DOF+1:2*DOF) = .TRUE.
			BD_MASK(2*DOF+1:3*DOF) = .TRUE.
		END WHERE
	ELSEIF (BCTYPE.EQ.'SCSC') THEN
		!!!!!!!!!! HARD TYPE SIMPLY SUPPORTED AND CLAMPED EDGE ALONG OPPOSITE TWO SIDES FOR EACH !!!!!!!!!!
		!---- W0 = 0, PIX = 0 ON EVERY BOUNDARIES -----		
		WHERE (EDGE.NE.'NN')
			BD_MASK(1:DOF) = .TRUE.
			BD_MASK(DOF+1:2*DOF) = .TRUE.
		END WHERE
		!----- PIY = 0 ON ON GAMMA 2 AND 4 -----
		WHERE (EDGE.EQ.'RR' .OR. EDGE.EQ.'LL' .OR. & 
			   EDGE.EQ.'LF' .OR. EDGE.EQ.'RF' .OR. EDGE.EQ.'LK' .OR. EDGE.EQ.'RK')
			BD_MASK(2*DOF+1:3*DOF) = .TRUE.
		END WHERE
	ENDIF

	!----- REDUCED MATRIX SIZE WITHOUT ROWS AND COLUMNS CORRESPONDING TO BOUNDARY NODES -----
	REDUCEDOF = 3*DOF - COUNT(BD_MASK)
	
END SUBROUTINE INFONODE

SUBROUTINE REDUCEMATRIX(THIN_K, THIN_KG, THIN_M, K, KG, M, REDUCEDOF, BD_MASK)
	INTEGER, INTENT(IN) :: REDUCEDOF
	LOGICAL, INTENT(IN) :: BD_MASK(3*DOF)
	TYPE(SUBMAT), INTENT(IN) :: K
	REAL*8, INTENT(IN) :: M(INT(0.5*DOF*(DOF+1))), KG(INT(0.5*DOF*(DOF+1)))
	REAL*8, INTENT(OUT) :: THIN_K(REDUCEDOF, REDUCEDOF), THIN_KG(REDUCEDOF, REDUCEDOF), THIN_M(REDUCEDOF, REDUCEDOF)
	
	REAL*8, ALLOCATABLE :: FAT_K(:,:), FAT_M(:,:), FAT_KG(:,:)
	INTEGER :: I, J, II, JJ, KK
	
	ALLOCATE(FAT_K(3*DOF,3*DOF), FAT_M(3*DOF, 3*DOF), FAT_KG(3*DOF,3*DOF))

	FAT_K(:,:) = 0.D0; FAT_KG(:,:) = 0.D0; FAT_M(:,:) = 0.D0
	
	!----- CONSTRUCT MATRIX [K] AND [M] WITOUHT ROWS AND COLUMNS CORRESPONDING TO BOUNDARY NODES -----

    PRINT *,
    PRINT *, 'CONSTRUCT REDUCED MATRIX [K], [KG] AND [M] ASSOCIATED WITH BOUNDARY CONDITION'

	!----- CONSTRUCT FULL STIFFNESS MATRIX, FAT_K [K] -----
	FORALL (I=1:K%NZ(1))
		FAT_K(K%R11(I), K%C11(I)) = K%K11(I)
		FAT_K(K%C11(I), K%R11(I)) = K%K11(I)
	END FORALL

	FORALL (I=1:K%NZ(2))
		FAT_K(DOF+K%R22(I), DOF+K%C22(I)) = K%K22(I)
		FAT_K(DOF+K%C22(I), DOF+K%R22(I)) = K%K22(I)
	END FORALL

	FORALL (I=1:K%NZ(3))
		FAT_K(2*DOF+K%R33(I), 2*DOF+K%C33(I)) = K%K33(I)
		FAT_K(2*DOF+K%C33(I), 2*DOF+K%R33(I)) = K%K33(I)
	END FORALL

	FORALL (I=1:K%NZ(4))
		FAT_K(K%R12(I), DOF+K%C12(I)) = K%K12(I)
		FAT_K(DOF+K%C12(I), K%R12(I)) = K%K12(I)
	END FORALL

	FORALL (I=1:K%NZ(5))
		FAT_K(K%R13(I), 2*DOF+K%C13(I)) = K%K13(I)
		FAT_K(2*DOF+K%C13(I), K%R13(I)) = K%K13(I)
	END FORALL

	FORALL (I=1:K%NZ(6))
		FAT_K(DOF+K%R23(I), 2*DOF+K%C23(I)) = K%K23(I)
		FAT_K(2*DOF+K%C23(I), DOF+K%R23(I)) = K%K23(I)
	END FORALL

	!----- CONSTRUCT FULL GEOMETRICAL STIFFNESS MATRIX, FAT_KG [KG] -----
	KK = 1
	DO I=1, DOF
		DO J=I, DOF
			! [KG11]
			FAT_KG(I,J) = KG(KK)
			FAT_KG(J,I) = FAT_KG(I,J)
			! [KG22]
   			FAT_KG(DOF+I, DOF+J) = (h**3 / 12.0D0)*KG(KK)
   			FAT_KG(DOF+J, DOF+I) = FAT_KG(DOF+I, DOF+J)
   			! [KG33]
   			FAT_KG(2*DOF+I, 2*DOF+J) = FAT_KG(DOF+I, DOF+J)
   			FAT_KG(2*DOF+J, 2*DOF+I) = FAT_KG(DOF+I, DOF+J)
			
			KK = KK + 1
			
		ENDDO
	ENDDO

	!----- CONSTRUCT FULL MASS MATRIX, FAT_M [M] -----
	KK = 1
	DO I=1, DOF
		DO J=I, DOF
			! [M11]
			FAT_M(I,J) = (RHO*h)*M(KK)
			FAT_M(J,I) = FAT_M(I,J)
			! [M22]
			FAT_M(DOF+I, DOF+J) = (RHO*h**3 / 12.0D0)*M(KK)
			FAT_M(DOF+J, DOF+I) = FAT_M(DOF+I, DOF+J)
			! [M33]
			FAT_M(2*DOF+I, 2*DOF+J) = FAT_M(DOF+I, DOF+J)
			FAT_M(2*DOF+J, 2*DOF+I) = FAT_M(DOF+I, DOF+J)
			
			KK = KK + 1
			
		ENDDO
	ENDDO
	
	II = 0; JJ = 0;
	     
	DO I=1, 3*DOF
		IF (BD_MASK(I).EQV..FALSE.) THEN
			II = II + 1
			JJ = II - 1
			DO J=I, 3*DOF
				IF (BD_MASK(J).EQV..FALSE.) THEN
					JJ = JJ + 1
					THIN_K(II,JJ) = FAT_K(I,J)
					THIN_K(JJ,II) = THIN_K(II,JJ)
				ENDIF
			ENDDO
		ENDIF
	ENDDO
	
	II = 0; JJ = 0;
	
	DO I=1, DOF
		IF (BD_MASK(I).EQV..FALSE.) THEN
			II = II + 1
			JJ = II - 1
			DO J=I, DOF
				IF (BD_MASK(J).EQV..FALSE.) THEN
					JJ = JJ + 1
					THIN_M(II,JJ) = FAT_M(I,J)
					THIN_M(JJ,II) = THIN_M(II,JJ)
					
					THIN_KG(II,JJ) = FAT_KG(I,J)
					THIN_KG(JJ,II) = THIN_KG(II,JJ)
				ENDIF
			ENDDO
		ENDIF
	ENDDO
	
	DO I=DOF+1, 2*DOF
		IF (BD_MASK(I).EQV..FALSE.) THEN
			II = II + 1
			JJ = II - 1
			DO J=I, 2*DOF
				IF (BD_MASK(J).EQV..FALSE.) THEN
					JJ = JJ + 1
					THIN_M(II,JJ) = FAT_M(I,J)
					THIN_M(JJ,II) = THIN_M(II,JJ)
					
					THIN_KG(II,JJ) = FAT_KG(I,J)
					THIN_KG(JJ,II) = THIN_KG(II,JJ)
				ENDIF
			ENDDO
		ENDIF
	ENDDO
	
	DO I=2*DOF+1, 3*DOF
		IF (BD_MASK(I).EQV..FALSE.) THEN
			II = II + 1
			JJ = II - 1
			DO J=I, 3*DOF
				IF (BD_MASK(J).EQV..FALSE.) THEN
					JJ = JJ + 1
					THIN_M(II,JJ) = FAT_M(I,J)
					THIN_M(JJ,II) = THIN_M(II,JJ)
					
					THIN_KG(II,JJ) = FAT_KG(I,J)
					THIN_KG(JJ,II) = THIN_KG(II,JJ)
				ENDIF
			ENDDO
		ENDIF
	ENDDO
	
!  	DO I=1, REDUCEDOF
!  		DO J=1, REDUCEDOF
!  			IF ((I.EQ.J).AND.(DABS(THIN_KG(I,J)).LE.EPS)) THEN
!  				THIN_KG(I,J) = 1.0D0
!  			ENDIF
!  		ENDDO
!  	ENDDO
	
	IF (REDUCEDOF.NE.UBOUND(THIN_K,1).OR.REDUCEDOF.NE.UBOUND(THIN_K,2).OR.REDUCEDOF.NE.UBOUND(THIN_KG,2)) THEN
		PRINT*, "Oops! you got wrong when full matrix is reduced at boundary.f90"
		STOP
	ENDIF

!	OPEN(1, FILE = "thin_k.dat")
!	OPEN(2, FILE = "thin_m.dat")
!	DO I=1, REDUCEDOF
!		WRITE(1,*) (THIN_K(I,J), J=1, REDUCEDOF)
!		WRITE(2,*) (THIN_M(I,J), J=1, REDUCEDOF)
!	ENDDO
!	CLOSE(1)
!	CLOSE(2)
	
	DEALLOCATE(FAT_K, FAT_M, FAT_KG)
	
END SUBROUTINE REDUCEMATRIX

END MODULE BOUNDARY
