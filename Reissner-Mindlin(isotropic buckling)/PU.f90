! FILE WRITTEN BY HYUNJU KIM
!
! DATE : JAN TUE 6 2009
!
! PARTITION OF UNITY FUNCTION CODE
!
! INPUT : POINT IN PHYSICAL 1-DIM OR POINT IN PHYSICAL 3-DIM OR VECTOR IN PHYSICAL 3-DIM
!               AND THE SUPPORT OF THE PARTITION OF UNITY FUNCTION
!
! OUTPUT : THE VALES OF THE FUNCTION AND PARTIAL DERIVATIVE OF THE FUNCTION

MODULE PU

    USE GLBVAR
    USE NEWTYPE

    IMPLICIT NONE

    TYPE PUMAP
        REAL*8 :: ZERO, ONE
    END TYPE PUMAP

    INTERFACE PHYPU
        MODULE PROCEDURE PHYPU1D, PHYPU2D, PHYPUVEC2D, PHYPUVEC1D
    END INTERFACE PHYPU
    
CONTAINS


!--------------------------PHYSICAL PU-2D-----------------------

SUBROUTINE PHYPUVEC2D(VECVAL, VEC, NVEC,  RECSUPPORT)

    INTEGER, INTENT(IN) :: NVEC
    TYPE(FVALUE), INTENT(OUT) :: VECVAL(NVEC)
    TYPE(POINT2D), INTENT(IN) :: VEC(NVEC)
    TYPE(RECPATCH), INTENT(IN) :: RECSUPPORT
    TYPE(PUMAP) :: MAPX, MAPY
    INTEGER :: I

    DO I=1,NVEC
        MAPX=PUMAPPINGX(VEC(I)%X, RECSUPPORT)
        MAPY=PUMAPPINGY(VEC(I)%Y, RECSUPPORT)

        VECVAL(I)%D00=PU1D(MAPX%ZERO, 0)*PU1D(MAPY%ZERO, 0)
        VECVAL(I)%D10=MAPX%ONE*PU1D(MAPX%ZERO, 1)*PU1D(MAPY%ZERO, 0)
        VECVAL(I)%D01=MAPY%ONE*PU1D(MAPX%ZERO, 0)*PU1D(MAPY%ZERO, 1)
    ENDDO

END SUBROUTINE PHYPUVEC2D

!--------------------------PHYSICAL PU-1D-----------------------
SUBROUTINE PHYPUVEC1D(VECVAL_ZERO, VECVAL_ONE, VEC, NVEC,  RECSUPPORT)

    INTEGER, INTENT(IN) :: NVEC
    REAL*8, INTENT(OUT) :: VECVAL_ZERO(2,NVEC), VECVAL_ONE(2,NVEC)
    TYPE(POINT2D), INTENT(IN) :: VEC(NVEC)
    TYPE(RECPATCH), INTENT(IN) :: RECSUPPORT
    TYPE(PUMAP) :: MAPX, MAPY
    INTEGER :: I

    DO I=1,NVEC
        MAPX=PUMAPPINGX(VEC(I)%X, RECSUPPORT)
        MAPY=PUMAPPINGY(VEC(I)%Y, RECSUPPORT)

        VECVAL_ZERO(1,I)=PU1D(MAPX%ZERO, 0)
        VECVAL_ZERO(2,I)=PU1D(MAPY%ZERO, 0)
        VECVAL_ONE(1,I)=MAPX%ONE*PU1D(MAPX%ZERO, 1)
        VECVAL_ONE(2,I)=MAPY%ONE*PU1D(MAPY%ZERO, 1)
    ENDDO

END SUBROUTINE PHYPUVEC1D

!--------------------------PHYSICAL PU-2D-----------------------

TYPE(FVALUE) FUNCTION PHYPU2D(PT2D,  RECSUPPORT)

    TYPE(POINT2D), INTENT(IN) :: PT2D
    TYPE(RECPATCH), INTENT(IN) :: RECSUPPORT
    TYPE(PUMAP) :: MAPX, MAPY

    MAPX=PUMAPPINGX(PT2D%X, RECSUPPORT)
    MAPY=PUMAPPINGY(PT2D%Y, RECSUPPORT)

    PHYPU2D%D00=PU1D(MAPX%ZERO, 0)*PU1D(MAPY%ZERO, 0)
    PHYPU2D%D10=MAPX%ONE*PU1D(MAPX%ZERO, 1)*PU1D(MAPY%ZERO,  0)
    PHYPU2D%D01=MAPY%ONE*PU1D(MAPX%ZERO, 0)*PU1D(MAPY%ZERO,  1)

END FUNCTION PHYPU2D

!--------------------------PHYSICAL PU-1D-----------------------------

REAL*8 FUNCTION PHYPU1D(X,  RECSUPPORT, FLAGX)

    REAL*8, INTENT(IN) :: X
    TYPE(RECPATCH), INTENT(IN) :: RECSUPPORT
    INTEGER, INTENT(IN) :: FLAGX
    TYPE(PUMAP) :: MAPX

    MAPX=PUMAPPINGX(X, RECSUPPORT)

    IF (FLAGX.EQ.0) THEN
        PHYPU1D=PU1D(MAPX%ZERO,  FLAGX)
    ELSEIF (FLAGX.EQ.1) THEN
        PHYPU1D=MAPX%ONE*PU1D(MAPX%ZERO, FLAGX)
    ENDIF

END FUNCTION PHYPU1D

!------------------------------------PU-1D----------------------------------------

REAL*8 FUNCTION PU1D(X, FLAGX)

	REAL*8, INTENT(IN) :: X
	INTEGER, INTENT(IN) :: FLAGX

	IF (FLAGX.EQ. 0) THEN													! PU-FUNCTION
		IF ((X + 1.0D0 .GE. EPS) .AND. (X .LT. EPS)) THEN
			IF (GORDER.EQ.1) THEN
				PU1D=(1.0D0+X)
			ELSEIF (GORDER.EQ.2) THEN
				PU1D=((1.0D0+X)**2)*(1.0D0-2.0D0*X)
			ELSEIF (GORDER.EQ.3) THEN
				PU1D=((1.0D0+X)**3)*(1.0D0-3.0D0*X+6.0D0*X**2)
			ELSEIF (GORDER.EQ.4) THEN
				PU1D=((1.0D0+X)**4)*(1.0D0-4.0D0*X+10.0D0*X**2-20.0D0*X**3)
			ELSEIF (GORDER.EQ.5) THEN
				PU1D=((1.0D0+X)**5)*(1.0D0-5.0D0*X+15.0D0*X**2-35.0D0*X**3+70.0D0*X**4)
			ELSEIF (GORDER.EQ.6) THEN
				PU1D=((1.0D0+X)**6.0D0)*(1.0D0-6.0D0*X+21.0D0*X**2.0D0-56.0D0*X**3.0D0+126.0D0*X**4.0D0 &
				-252.0D0*X**5.0D0)
			ELSEIF (GORDER.EQ.7) THEN
				PU1D=((1.0D0+X)**7)*(1.0D0-7.0D0*X+28.0D0*X**2-84.0D0*X**3+210.0D0*X**4-462.0D0*X**5 &
				+924.0D0*X**6)
			ENDIF
		ELSEIF ((X .GE. EPS) .AND. (X - 1.0D0 .LT. EPS)) THEN
			IF (GORDER.EQ.1) THEN
				PU1D=(1.0D0-X)
			ELSEIF (GORDER.EQ.2) THEN
				PU1D=((1.0D0-X)**2)*(1.0D0+2.0D0*X)
			ELSEIF (GORDER.EQ.3) THEN
				PU1D=((1.0D0-X)**3)*(1.0D0+3.0D0*X+6.0D0*X**2)
			ELSEIF (GORDER.EQ.4) THEN
				PU1D=((1.0D0-X)**4)*(1.0D0+4.0D0*X+10.0D0*X**2+20.0D0*X**3)
			ELSEIF (GORDER.EQ.5) THEN
				PU1D=((1.0D0-X)**5)*(1.0D0+5.0D0*X+15.0D0*X**2.0D0+35.0D0*X**3.0D0+70.0D0*X**4)
			ELSEIF (GORDER.EQ.6) THEN
				PU1D=((1.0D0-X)**6)*(1.0D0+6.0D0*X+21.0D0*X**2+56.0D0*X**3+126.0D0*X**4+252.0D0*X**5)
			ELSEIF (GORDER.EQ.7) THEN
				PU1D=((1.0D0-X)**7)*(1.0D0+7.0D0*X+28.0D0*X**2+84.0D0*X**3+210.0D0*X**4+462.0D0*X**5 &
				+924.0D0*X**6)
			ENDIF
		ELSE
			PU1D=0.0D0
		ENDIF
	ELSEIF (FLAGX.EQ.1) THEN												! DIFFERENTIATION OF PU-FUNCTION
		IF ((X + 1.0D0 .GE. EPS) .AND. (X .LT. EPS)) THEN
			IF (GORDER.EQ.1) THEN
				PU1D=1.0D0
			ELSEIF (GORDER.EQ.2) THEN
				PU1D=-6.0D0*X*(1.0D0 + X)
			ELSEIF (GORDER.EQ.3) THEN
				PU1D=30.0D0*X**2*(1.0D0 + X)**2
			ELSEIF (GORDER.EQ.4) THEN
				PU1D=-140.0D0*X**3*(1.0D0 + X)**3
			ELSEIF (GORDER.EQ.5) THEN
				PU1D=630.0D0*X**4*(1.0D0 + X)**4
			ELSEIF (GORDER.EQ.6) THEN
				PU1D=-2772.0D0*X**5*(1.0D0 + X)**5
			ELSEIF (GORDER.EQ.7) THEN
				PU1D=12012.0D0*X**6*(1.0D0 + X)**6
			ENDIF
		ELSEIF ((X .GT. EPS) .AND. (X - 1.0D0 .LE. EPS)) THEN
			IF (GORDER.EQ.1) THEN
				PU1D=-1.0D0
			ELSEIF (GORDER.EQ.2) THEN
				PU1D=6.0D0*(-1.0D0 + X)*X
			ELSEIF (GORDER.EQ.3) THEN
				PU1D=-30.0D0*(-1.0D0 + X)**2*X**2
			ELSEIF (GORDER.EQ.4) THEN
				PU1D=140.0D0*(-1.0D0 + X)**3*X**3
			ELSEIF (GORDER.EQ.5) THEN
				PU1D=-630.0D0*(-1.0D0 + X)**4*X**4
			ELSEIF (GORDER.EQ.6) THEN
				PU1D=2772.0D0*(-1.0D0 + X)**5*X**5
			ELSEIF (GORDER.EQ.7) THEN
				PU1D=-12012.0D0*(-1.0D0 + X)**6*X**6
			ENDIF
        ELSE
            PU1D=0.0D0
		ENDIF
	ELSEIF (FLAGX.EQ.2) THEN												! DIFFERENTIATION OF PU-FUNCTION
		IF ((X + 1.0D0 .GE. EPS) .AND. (X .LT. EPS)) THEN
			IF (GORDER.EQ.1) THEN
				PU1D=0.0D0
			ELSEIF (GORDER.EQ.2) THEN
				PU1D=-6*x - 6*(1.0D0 + x)
			ELSEIF (GORDER.EQ.3) THEN
				PU1D=60*x**2*(1.0D0 + X) + 60*x*(1.0D0 + X)**2
			ELSEIF (GORDER.EQ.4) THEN
				PU1D=-420*x**3*(1.0D0 + X)**2 - 420*x**2*(1.0D0 + X)**3
			ELSEIF (GORDER.EQ.5) THEN
				PU1D=2520*x**4*(1.0D0 + X)**3 + 2520*x**3*(1.0D0 + X)**4
			ELSEIF (GORDER.EQ.6) THEN
				PU1D=-13860*x**5*(1.0D0 + X)**4 - 13860*x**4*(1.0D0 + X)**5
			ELSEIF (GORDER.EQ.7) THEN
				PU1D=72072*x**6*(1.0D0 + X)**5 + 72072*x**5*(1.0D0 + X)**6
			ENDIF
		ELSEIF ((X .GT. EPS) .AND. (X - 1.0D0 .LE. EPS)) THEN
			IF (GORDER.EQ.1) THEN
				PU1D=0.0D0
			ELSEIF (GORDER.EQ.2) THEN
				PU1D=6*x + 6*(-1.0D0 + X)
			ELSEIF (GORDER.EQ.3) THEN
				PU1D=-60*x**2*(-1.0D0 + X) - 60*x*(-1.0D0 + X)**2
			ELSEIF (GORDER.EQ.4) THEN
				PU1D=420*x**3*(-1.0D0 + X)**2 + 420*x**2*(-1.0D0 + X)**3
			ELSEIF (GORDER.EQ.5) THEN
				PU1D=-2520*x**4*(-1.0D0 + X)**3 - 2520*x**3*(-1.0D0 + X)**4
			ELSEIF (GORDER.EQ.6) THEN
				PU1D=13860*x**5*(-1.0D0 + X)**4 + 13860*x**4*(-1.0D0 + X)**5
			ELSEIF (GORDER.EQ.7) THEN
				PU1D=-72072*x**6*(-1.0D0 + X)**5 - 72072*x**5*(-1.0D0 + X)**6
			ENDIF
        ELSE
            PU1D=0.0D0
		ENDIF
		IF (DABS(X).LE.EPS) THEN
            PU1D=0.0D0
        ENDIF
	ENDIF
END FUNCTION PU1D

!-------------------------------PU-MAPPING, X-AXIS---------------------------------

TYPE(PUMAP) FUNCTION PUMAPPINGX(X, RECSUPPORT)

    TYPE(RECPATCH), INTENT(IN) :: RECSUPPORT !<-----SUPPORT OF PU FUNCTION
    REAL*8, INTENT(IN) :: X
    TYPE(RECPATCH) :: CENTERSUBPATCH
    REAL*8 :: XPTS(4)
	
    CENTERSUBPATCH = RECSUPPORT +(-2*DELTA)

    XPTS(1)=RECSUPPORT%PT1%X
    XPTS(2)=CENTERSUBPATCH%PT1%X
    XPTS(3)=CENTERSUBPATCH%PT2%X
    XPTS(4)=RECSUPPORT%PT2%X

    IF ((X-XPTS(2).GE.EPS).AND.(X-XPTS(3).LE.EPS)) THEN
        PUMAPPINGX%ZERO=0.0D0
        PUMAPPINGX%ONE=0.0D0
    ELSEIF ((X-XPTS(1).GE.EPS).AND.(X-XPTS(2).LE.EPS)) THEN
        PUMAPPINGX%ZERO=(X-XPTS(2))/(XPTS(2)-XPTS(1))
        PUMAPPINGX%ONE=1.0D0/(XPTS(2)-XPTS(1))
    ELSEIF ((X-XPTS(3).GE.EPS).AND.(X-XPTS(4).LE.EPS)) THEN
        PUMAPPINGX%ZERO=(X-XPTS(3))/(XPTS(4)-XPTS(3))
        PUMAPPINGX%ONE=1.0D0/(XPTS(4)-XPTS(3))
    ELSE
        PUMAPPINGX%ZERO=5.0D0
        PUMAPPINGX%ONE=0.0D0
    ENDIF

    IF ((DABS(X-XPTS(2)).LE.EPS) .OR. (DABS(X-XPTS(3)).LE.EPS)) THEN
        PUMAPPINGX%ZERO=0.0D0
        PUMAPPINGX%ONE=0.0D0
    ENDIF

     IF ((DABS(X-XPTS(1)).LE.EPS) .OR. (DABS(X-XPTS(4)).LE.EPS)) THEN
        PUMAPPINGX%ZERO=5.0D0
        PUMAPPINGX%ONE=0.0D0
     ENDIF

END FUNCTION PUMAPPINGX

!-------------------------------PU-MAPPING, Y-AXIS---------------------------------

TYPE(PUMAP) FUNCTION PUMAPPINGY(Y, RECSUPPORT)

    TYPE(RECPATCH), INTENT(IN) :: RECSUPPORT !<-----SUPPORT OF THE PU FUNCTION
    REAL*8, INTENT(IN) :: Y
    TYPE(RECPATCH) :: CENTERSUBPATCH
    REAL*8 :: YPTS(4)
    TYPE(RECPATCH) :: TESTSUPPORT, TESTSUBPATCH

    CENTERSUBPATCH = RECSUPPORT + (-2*DELTA)

    YPTS(1)=RECSUPPORT%PT1%Y
    YPTS(2)=CENTERSUBPATCH%PT1%Y
    YPTS(3)=CENTERSUBPATCH%PT4%Y
    YPTS(4)=RECSUPPORT%PT4%Y

    IF ((Y-YPTS(2).GE.EPS).AND.(Y-YPTS(3).LE.EPS)) THEN
        PUMAPPINGY%ZERO=0.0D0
        PUMAPPINGY%ONE=0.0D0
    ELSEIF ((Y-YPTS(1).GE.EPS).AND.(Y-YPTS(2).LE.EPS)) THEN
        PUMAPPINGY%ZERO=(Y-YPTS(2))/(YPTS(2)-YPTS(1))
        PUMAPPINGY%ONE=1.0D0/(YPTS(2)-YPTS(1))
    ELSEIF ((Y-YPTS(3).GE.EPS).AND.(Y-YPTS(4).LE.EPS)) THEN
        PUMAPPINGY%ZERO=(Y-YPTS(3))/(YPTS(4)-YPTS(3))
        PUMAPPINGY%ONE=1.0D0/(YPTS(4)-YPTS(3))
    ELSE
        PUMAPPINGY%ZERO=5.0D0
        PUMAPPINGY%ONE=0.0D0
    ENDIF

    IF ((DABS(Y-YPTS(2)).LE.EPS) .OR. (DABS(Y-YPTS(3)).LE.EPS)) THEN
        PUMAPPINGY%ZERO=0.0D0
        PUMAPPINGY%ONE=0.0D0
    ENDIF

     IF ((DABS(Y-YPTS(1)).LE.EPS) .OR. (DABS(Y-YPTS(4)).LE.EPS)) THEN
        PUMAPPINGY%ZERO=5.0D0
        PUMAPPINGY%ONE=0.0D0
     ENDIF

END FUNCTION PUMAPPINGY

END MODULE PU
